plugins {
  id 'maven-publish'
  id("org.owasp.dependencycheck") version "8.4.2"
}

group "com.kodality.kefhir"

allprojects {
  apply plugin: "java-library"
  apply plugin: "org.owasp.dependencycheck"

  version rootProject.version
  group rootProject.group

  configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
  }

  sourceCompatibility = 17
  targetCompatibility = 17

  repositories {
    mavenCentral()
    maven { url "https://kexus.kodality.com/repository/maven-public/" }
  }

  dependencies {
    annotationProcessor "org.projectlombok:lombok:1.18.30"
    compileOnly "org.projectlombok:lombok:1.18.30"

    annotationProcessor platform("io.micronaut:micronaut-bom:$micronautVersion")
    annotationProcessor "io.micronaut:micronaut-inject-java"

    api platform("io.micronaut:micronaut-bom:$micronautVersion")
    implementation "io.micronaut:micronaut-inject"
    implementation "io.micronaut:micronaut-runtime"
    implementation 'jakarta.inject:jakarta.inject-api:2.0.1'
    implementation "com.fasterxml.jackson.core:jackson-databind:${jacksonVersion}"

    implementation "org.apache.commons:commons-collections4:4.4"
    implementation "org.apache.commons:commons-lang3:3.13.0"
    implementation "commons-io:commons-io:2.15.0"
  }
}

subprojects { Project project ->
  apply plugin: "maven-publish"

  java {
    withSourcesJar()
  }

  publishing {
    publications {
      mavenJava(MavenPublication) {
        artifactId = project.name
        from components.java
      }
    }
    repositories {
      maven {
        def releasesRepoUrl = "https://kexus.kodality.com/repository/maven-releases/"
        def snapshotsRepoUrl = "https://kexus.kodality.com/repository/maven-snapshots/"
        url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
        credentials {
          username = project.hasProperty('kodalityNexusUser') ? project.property('kodalityNexusUser') : System.getenv('KODALITY_NEXUS_USER')
          password = project.hasProperty('kodalityNexusPassword') ? project.property('kodalityNexusPassword') : System.getenv('KODALITY_NEXUS_PASSWORD')
        }
      }
    }
  }

}
